def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY');
def buildAsApplication = !buildAsLibrary
if (buildAsApplication) {
    apply plugin: 'com.android.application'
}
else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'

android {
    namespace 'com.dishii.zelda3'
    compileSdkVersion 35
    defaultConfig {
        if (buildAsApplication) {
            applicationId "com.dishii.zelda3"
        }
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 2
        versionName "1.1"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_PLATFORM=android-24",
                          "-DANDROID_STL=c++_shared",
                          "-DSDL2MIXER_INSTALL=OFF",
                          "-DSDL2MIXER_SAMPLES=OFF",
                          "-DSDL2MIXER_VENDORED=ON",
                          "-DSDL2MIXER_OPUS=OFF",
                          "-DSDL2MIXER_MOD=OFF",
                          "-DSDL2MIXER_MIDI=OFF",
                          "-DSDL2MIXER_VORBIS_STB=ON",
                          "-DSDL2MIXER_VORBIS_TREMOR=OFF",
                          "-DSDL2MIXER_VORBIS_VORBISFILE=OFF",
                          "-DSDL2MIXER_FLAC_DRFLAC=ON",
                          "-DSDL2MIXER_FLAC_LIBFLAC=OFF",
                          "-DSDL2MIXER_MP3_DRMP3=ON",
                          "-DSDL2MIXER_MP3_MPG123=OFF"
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            jniDebuggable false  // Set to true only when debugging native code
        }
    }
    applicationVariants.all { variant ->
        tasks["merge${variant.name.capitalize()}Assets"]
            .dependsOn("externalNativeBuild${variant.name.capitalize()}")
    }
    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }
        externalNativeBuild {
            cmake {
                path 'jni/CMakeLists.txt'
                version '3.22.1'
            }
        }

    }
    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    if (buildAsLibrary) {
        libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith(".aar")) {
                    def fileName = "org.libsdl.app.aar";
                    output.outputFile = new File(outputFile.parent, fileName);
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.1.0'
    implementation 'androidx.window:window:1.3.0'
}
